
Ссылки:

- [[XDTO#Тип СериализаторXDTO|Сериализатор XDTO]]
- [[02 Программирование/02 1С - Предприятие/02 Встроенный язык/Сериализация/JSON#Тип ЗаписьJSON|Запись JSON]]

## Сериализация объекта конфигурации

К сожалению, в языке 1С нет универсального способа сериализации объекта любого типа в JSON, как в нормальнызыках (таких как, к примеру C#). По этой причине для каждого сложного типа нужно писать свой алгоритм сериализации.

Сериализация структуры

```bsl
&НаКлиенте
Функция СтруктураВСтрокуJSON(структура)
    ЗаписьJSON = новый ЗаписьJSON;
    ЗаписьJSON.УстановитьСтроку(); // режим записи в строковую переменную
    ЗаписатьJSON(ЗаписьJSON, структура);
    
    JSON = ЗаписьJSON.Закрыть();
    возврат JSON;
КонецФункции
```

Десериализация структуры из JSON

```bsl
&НаКлиенте
Функция ПолучитьСтруктуру(строкаJSON)
    ЧтениеJSON = новый ЧтениеJSON;
    ЧтениеJSON.УстановитьСтроку(строкаJSON);
    СтруктураИзJSON = ПрочитатьJSON(ЧтениеJSON);
    ЧтениеJSON.Закрыть();
    
    возврат СтруктураИзJSON;
КонецФункции
```

В JSON могут быть записаны значения следующих типов:

- **`Неопределено`**
- **`Null`**
- **`Булево`**
- **`Число`**
- **`Строка`**
- **`Дата`**
- **`Тип`**
- **`Массив`**
- **`Структура`**
- **`Соответствие`**
- **`УникальныйИдентификатор`**
- **`ДвоичныеДанные`**
- **`ХранилищеЗначения`**
- **`ОписаниеТипов`**
- **`Объекты данных`**
- **`Наборы записей регистров`**
- **`Менеджер значения константы`**

>В принципе, любой объект можно преобразовать в структуру, а коллекцию в свойство структуры

В дальнейшем список может пополняться. При попытке записи значения неподходящего типа будет вызвано исключение

Если нужно сериализовать ТаблицуЗначений, можно легко ее преобразовать в массив структур с помощью БСП

```bsl
массивСтруктур = ОбщегоНазначения.ТаблицаЗначенийВМассив(табЗнач);
```

**Сериализация массива, состоящего из 3 простых элементов и 1 объекта с двумя свойствами**

```bsl
ЗаписьJSON = Новый ЗаписьJSON;
ЗаписьJSON.УстановитьСтроку();

ЗаписьJSON.ЗаписатьНачалоМассива();
    ЗаписьJSON.ЗаписатьЗначение("текст");
    ЗаписьJSON.ЗаписатьЗначение(12.345, Истина);
    ЗаписьJSON.ЗаписатьЗначение(Истина);
    
    ЗаписьJSON.ЗаписатьНачалоОбъекта();
        ЗаписьJSON.ЗаписатьИмяСвойства("Свойство1");
        ЗаписьJSON.ЗаписатьЗначение("Значение1");
        
        ЗаписьJSON.ЗаписатьИмяСвойства("Свойство2");
        ЗаписьJSON.ЗаписатьЗначение(неопределено);        
    ЗаписьJSON.ЗаписатьКонецОбъекта();
    
ЗаписьJSON.ЗаписатьКонецМассива();

JSON = ЗаписьJSON.Закрыть();
```

Получится такой JSON объект:

```json
[
    "текст",
    1.2345E1,
    true,
    {
        "Свойство1": "Значение1",
        "Свойство2": null
    }
]
```

**Сериализация одиночной структуры**

```bsl
ЗаписьJSON = Новый ЗаписьJSON;
ЗаписьJSON.УстановитьСтроку(); // Инициализация для записи JSON в строковую переменную

Структура = Новый Структура;
Структура.Вставить("Поле1", "Значение1");
Структура.Вставить("Поле2", "Значение2");

ЗаписатьJSON(ЗаписьJSON, Структура); // Сериализация структуры в JSON
СтрокаJSON = ЗаписьJSON.Закрыть();   // Получение итоговой строки JSON
```

Сериализация объекта конфигурации напрямую недопустима, сначала нужно сериализовать объект

```bsl
ЗаписьJSON = Новый ЗаписьJSON;
ЗаписьJSON.УстановитьСтроку();

структураОбъекта = новый Структура();
ЗаполнитьЗначенияСвойств(структураОбъекта, объектКонфы);
ЗаписатьJSON(ЗаписьJSON, структураОбъекта);
JSON = ЗаписьJSON.Закрыть();
```

Еще можно применять сериализацию ссылки в XML, чтобы уже потом строку XML записывать как значение свойства JSON.
Вставка сериализованной ссылки на справочник Изделия как значение свойства "Изделие"

```
структураОбъекта.Вставить("Изделие", XMLСтрока(объектКонфы.Изделие));

ЗаписьJSON = Новый ЗаписьJSON;
ЗаписьJSON.УстановитьСтроку();
ЗаписатьJSON(ЗаписьJSON, структураОбъекта);
JSON = ЗаписьJSON.Закрыть();
```

### Тип ЗаписьJSON

>Доступен на клиенте и на сервере

| Свойтсво           | Описание                                                                                                                                                                                                                                                                                                                                                                                                                                                               |
| ------------------ | ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------- |
| Параметры          | Тип: **`ПараметрыЗаписиJSON`**                                                                                                                                                                                                                                                                                                                                                                                                                                         |
| ПроверятьСтруктуру | **`Булево`**. Показывает, будет ли проводиться проверка правильности структуры записываемого JSON-объекта. В случае обнаружения ошибки, будет сгенерировано исключение. Например, при попытке записать значение без имени вне массива или записать окончание объекта без начала. Установка данного свойства не имеет немедленного эффекта. Установленное значение свойства будет использовано только после открытия файла или установки строки. По умолчанию: `Истина` |


| Метод                                                      | Описание                                                                                                                                                                             |
| ---------------------------------------------------------- | ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------ |
| `ОткрытьФайл(путь, кодировка,  добавлятьBOM, параметры)`   | Открывает файл для записи                                                                                                                                                            |
| `ОткрытьПоток(поток, кодировка,  добавлятьBOM, параметры)` | Тип: **`Поток`**, **`ПотокВПамяти`**, **`ФайловыйПоток`**. Поток для записи.                                                                                                         |
| `Закрыть()`                                                | Завершение записи JSON, если запись производилась в файл, то возвращается пустая строка, иначе возвращается текст JSON                                                               |
| `УстановитьСтроку()`                                       | Активация режима записи в строку, а не в файл или поток (но это не точно)                                                                                                            |
| `ЗаписатьБезОбработки()`                                   | Выполняет запись произвольной строки в документ, проверка структуры документа не выполняется                                                                                         |
| `ЗаписатьИмяСвойства(имя)`                                 | **Записывает имя свойства** в текст JSON                                                                                                                                             |
| `ЗаписатьЗначение(значение, форматЭкспоненты)`             | значение: Строка, Число, Булево, Неопределено. форматЭкспоненты (Булево) определяет будет ли число записано в эксп. виде (если это число, конечно). **Записывает значение свойства** |
| `ЗаписатьНачалоМассива()`                                  | Записывает начало коллекции                                                                                                                                                          |
| `ЗаписатьКонецМассива()`                                   | Записывает конец коллекции                                                                                                                                                           |
| `ЗаписатьНачалоОбъекта()`                                  | Записывает начало объекта                                                                                                                                                            |
| `ЗаписатьКонецОбъекта()`                                   | Записывает конец объека                                                                                                                                                              |

(Плохой) Пример

```bsl
// Запись JSON вручную с прописыванием каждого ключа и значения!

ЗаписьJSON = Новый ЗаписьJSON;

ЗаписьJSON.ЗаписатьНачалоМассива();
    ЗаписьJSON.ЗаписатьЗначение("Строковое значение");
    ЗаписьJSON.ЗаписатьЗначение(123);
    
    ЗаписьJSON.ЗаписатьНачалоОбъекта();
        ЗаписьJSON.ЗаписатьИмяСвойства("Булево");
        ЗаписьJSON.ЗаписатьЗначение(Истина);
    ЗаписьJSON.ЗаписатьКонецОбъекта();
ЗаписьJSON.ЗаписатьКонецМассива();
```

#### Тип ПараметрыЗаписиJSON

| Свойтсво                     | Описание                                                                        |
| ---------------------------- | ------------------------------------------------------------------------------- |
| ИспользоватьДвойныеКавычки   | **`Булево`**                                                                    |
| ПереносСтрок                 | тип: **`ПереносСтрокJSON { Unix, Windows, Авто, Нет }`**                        |
| СимволыОтступа               | **`Строка`**. Применяется только, если `ПереносСтрок != Нет`. По умолчанию `""` |
| ЭкранированиеСимволов        | тип: **`ЭкранированиеСимволовJSON { Нет, СимволыВнеASCII, СимволыВнеBMP }`**    |
| ЭкранироватьАмперсанд        | **`Булево`**                                                                    |
| ЭкранироватьОдинарныеКавычки | **`Булево`**                                                                    |
| ЭкранироватьРазделителиСтрок | **`Булево`**                                                                    |
| ЭкранироватьСлеш             | **`Булево`**                                                                    |
| ЭкранироватьУгловыеСкобки    | **`Булево`**                                                                    |

Параметры конструктора все необязательные. Фактически конструктор на вход просто принимает значения всех параметров (свойств) типа.

#1С #1С/Язык_программирования/Сериализация/JSON #JSON
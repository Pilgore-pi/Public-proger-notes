[Хороший источник](https://www.youtube.com/watch?v=IDNoe_-fg_Y)

>Исключения и транзакции независимы между собой

Начало и завершение транзакции должно быть определено в одном и том же методе

Правила использования из официальной документации, которым всегда нужно следовать:

# Транзакции: правила использования


> _Область применения: управляемое приложение, мобильное приложение, обычное приложение._

Транзакции применяются для целостного изменения связанных данных, т.е. все действия с базой данных, выполняемые в рамках транзакции или выполняются целиком, или целиком откатываются.

1. Использование транзакций в **1С:Предприятии** обладает рядом особенностей:

- не поддерживаются вложенные транзакции (см. подробнее [Вложенность транзакций](https://its.1c.ru/db/metod8dev/content/2334/hdoc));
    
- при возникновении исключения в общем случае транзакция не может быть зафиксирована – при этом не важно, было ли это исключение обработано или нет (см. подробнее [Ошибки базы данных и транзакции](https://its.1c.ru/db/metod8dev/content/2313/hdoc), [Особенности работы объектов при отмене транзакции](https://its.1c.ru/db/metod8dev/content/2733/hdoc));
    
- транзакция может быть инициирована явно в прикладном коде при использовании метода **НачатьТранзакцию**. Так же платформа **1С:Предприятие** неявным образом начинает транзакцию при любой записи в базу данных (см. подробнее [Документация платформы. Механизм транзакций](https://its.1c.ru/db/v8313doc/bookmark/dev/TI000000527));


Эти особенности накладывают ряд требований к написанию кода с использованием транзакций. Несоблюдение этих требований может приводить к возникновению ошибок вида «В этой транзакции уже происходили ошибки», которые может быть крайне сложно воспроизвести и отладить.

1.1. Поскольку исключение не отменяет транзакцию сразу, но запрещает успешное завершение транзакции, то все вызовы **НачатьТранзакцию** с одной стороны и **ЗафиксироватьТранзакцию** или **ОтменитьТранзакцию** с другой стороны должны быть парными.

1.2. Начало транзакции и ее фиксация (отмена) должны происходить в контексте одного метода

**Правильно**

```bsl
Процедура ЗаписатьДанныеВИБ()

    НачатьТранзакцию();
    
    Попытка
        ... // чтение или запись данных
        ДокументОбъект.Записать()
        ЗафиксироватьТранзакцию();
    Исключение
        ОтменитьТранзакцию();
        ... // дополнительные действия по обработке исключения  
    КонецПопытки;

КонецПроцедуры
```

**Неправильно**

```bsl
Процедура ЗаписатьДанныеВИБ()  
    НачатьТранзакцию();  
    ЗаписатьДокумент();
КонецПроцедуры;

Процедура ЗаписатьДокумент()
    Попытка  
        ... // чтение или запись данных  
        ДокументОбъект.Записать()  
        ЗафиксироватьТранзакцию();  
    Исключение  
        ОтменитьТранзакцию();  
    ... // дополнительные действия по обработке исключения  
    КонецПопытки;

КонецПроцедуры
```

1.3. При использовании транзакций необходимо предусмотреть обработку исключений, придерживаясь следующих правил:

- метод **НачатьТранзакцию** должен быть за пределами блока **Попытка-Исключение** непосредственно перед оператором **Попытка**;

- все действия, выполняемые после вызова метода **НачатьТранзакцию**, должны находиться в одном блоке Попытка, в том числе чтение, блокировка и обработка данных;

- метод **ЗафиксироватьТранзакцию** должен идти последним в блоке **Попытка** перед оператором **Исключение**, чтобы  гарантировать, что после **ЗафиксироватьТранзакцию** не возникнет исключение;

- необходимо предусмотреть обработку исключений – в блоке **Исключение** нужно сначала вызвать метод **ОтменитьТранзакцию**, а затем выполнять другие действия, если они требуются;

- рекомендуется в блоке **Исключение** делать запись в [журнал регистрации](https://its.1c.ru/db/v8std/content/src/300/200/i8100498.htm_);

- при использовании вложенных транзакций (см. п. 1.4) в конце блока **Исключение** рекомендуется добавить оператор **ВызватьИсключение**.  В противном случае исключение не будет передано выше по стеку вызовов, там не сработает обработка исключения, внешняя транзакция не будет явным образом отменена и платформа вызовет исключение «В данной транзакции происходила ошибка»


Пример

```bsl
НачатьТранзакцию();  

Попытка  
    БлокировкаДанных = Новый БлокировкаДанных;  
    ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Документ.ПриходнаяНакладная");  
    ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", СсылкаДляОбработки);  
    ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;  
    БлокировкаДанных.Заблокировать();

    ... // чтение или запись данных  
  
    ДокументОбъект.Записать();

    ЗафиксироватьТранзакцию();
    
Исключение  
    ОтменитьТранзакцию();

    ЗаписьЖурналаРегистрации(НСтр("ru = 'Выполнение операции'"),  
        УровеньЖурналаРегистрации.Ошибка,  
        ,  
        ,  
        ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

    ВызватьИсключение; // есть внешняя транзакция

КонецПопытки;
```

1.4. Использование вложенных транзакций приводит к усложнению кода. Принимая решение об использовании этой возможности, нужно очень взвешенно оценить решаемую задачу: возможно, это усложнение просто не оправдано.

1.4.1. Не стоит усложнять код, явно используя метод **НачатьТранзакцию**, когда кроме записи объекта другие действия c базой данных не делаются – платформа при записи сама откроет транзакцию.

Не нужно явно открывать транзакцию тогда, когда не требуется выполнять [ответственное чтение данных](https://its.1c.ru/db/v8std/content/648/hdoc). Например, обычно ответственное чтение не требуется при записи нового объекта (нового набора записей регистра).

При использовании методов **ПолучитьОбъект** (или **Прочитать** для наборов записей) необходимо анализировать должно ли чтение быть отвественным и в зависимости от этого принимать решение о явном использовании метода **НачатьТранзакцию**.

Правильно

```bsl
Попытка  
    ДокументОбъект = Документы.ПриходнаяНакладная.СоздатьДокумент();  
    ... // действия по заполнению объекта  
    ДокументОбъект.Записать();  
Исключение  
    ... // действия по обработке исключения  
КонецПопытки;
```

Неправильно

```bsl
НачатьТранзакцию();  
Попытка  
    ДокументОбъект = Документы.ПриходнаяНакладная.СоздатьДокумент();  
    ... // действия по заполнению объекта  
    ДокументОбъект.Записать();  
    ЗафиксироватьТранзакцию();  
Исключение  
    ОтменитьТранзакцию();  
КонецПопытки;
```

1.4.2. Если метод рассчитан на вызов только в рамках уже открытой транзакции (например, метод предназначен для вызова только из событий **ПередЗаписью**, **ОбработкаПроведения** и т.п.) в общем случае явным образом открывать в нем транзакцию не имеет никакого практического смысла.

1.4.3. При необходимости повысить качество сообщений об ошибках – на каждом уровне разработчик может предусмотреть свою обработку исключений, для чего, возможно, потребуется открыть вложенную транзакцию.

Пример. Вызывается метод **ДобавитьЭлектроннуюПодпись**. Внутри, если что-то пошло не так, нужно обработать исключение и добавить текст вида: «Не удалось добавить электронную подпись к объекту %ПредставлениеОбъекта% по причине:%ОписаниеОшибки%». В противном случае исключение будет обработано выше по стеку вызовов, например, при записи файла и будет выдано сообщение вида: «Не удалось записать файл %ИмяФайла% по причине: %ОписаниеОшибки%», где в «%ОписаниеОшибки%», будет просто указание на строчку кода и пользователю будет непонятно, зачем вообще программа записывала файл, если он просто его подписывал.

1.4.4. При обработке исключения, если транзакция все еще активна, например, исключение возникло во вложенной транзакции, нельзя обращаться к базе данных, так как это приведет к исключению «В этой транзакции уже происходили ошибки». При этом нужно учитывать, что обращение к базе данных может быть неявным, например, для получения представления ссылки.

2. Ограничение на длину (продолжительность) транзакции.

2.1. В общем случае в рамках одной транзакции нужно выполнять только те действия, которые неделимы, исходя из бизнес-логики.

Пример. При проведении документа записывается документ и его движения в регистрах. Если не прошла запись хотя бы в один регистр вся операция проведения должна быть отменена.

2.1.1. Если с точки зрения бизнес-логики действия могут быть выполнены по отдельности, то их в общем случае не следует объединять в одну транзакцию.

2.1.2. Исключением из п.2.1.1 могут быть случаи, когда с целью оптимизации несколько несвязанных объектов обрабатываются в рамках одной транзакции. В этом случае необходимо взвешенно подходить к выбору порции обработки данных: нужно стремиться к достижению золотой середины между длительностью одной транзакции и объемом фиксируемых данных с одной стороны и количеством транзакций с другой.

2.2. Следует избегать транзакций, которые выполняются длительное время.

Например, неправильно: для загрузки адресного классификатора записывать все данные, относящиеся к одной версии классификатора в одной транзакции, для того, чтобы в случае ошибки откатить целиком загружаемую версию классификатора. Т.к. данных по одной версии классификатора много (объем около 1 Гб), то для выполнения такой транзакции, во-первых, может не хватить оперативной памяти (особенно при использовании файловой информационной базы на 32-разрядной ОС), а, во-вторых, такая операция будет выполняться достаточно долго и ее нельзя будет оптимизировать за счет выполнения в несколько потоков.

Правильно: разбить загрузку новой версии классификатора на небольшие порции так, чтобы запись порции в одной транзакции не превышала 20 секунд в условиях высоконагруженной информационной системы и реализовать функциональность по откату к предыдущей версии в случае ошибки. Максимальная продолжительность указана исходя из того, что время ожидания установки транзакционной блокировки данных в информационной базе по умолчанию равно 20 сек.

См. также [Особенности использования транзакций при обмене данными](https://its.1c.ru/db/metod8dev/content/2274/hdoc).

2.2.1 Чем дольше выполняется транзакция, тем большее время будут заняты ресурсы сервера **1С:Предприятия** и СУБД. Как правило длинные транзакции занимают следующие ресурсы:

- в ходе выполнения транзакции все изменения в базе данных записываются в журнал транзакций, что необходимо для возможности откатить транзакцию;
- блокировки, установленные в транзакции, остаются до конца транзакции;
- на сервере **1С:Предприятия** блокировки занимают оперативную память;
- другие ресурсы, необходимые самой бизнес-логике, которая выполняется в транзакции.

Все это в целом может снижать эффективность использования ресурсов.

2.2.2. Если две транзакции пересекаются по блокируемым ресурсам, то транзакция, которая начала выполняться позже, будет ожидать возможность установления блокировки ограниченное время (по умолчанию – 20 секунд), после чего будет завершена с исключением «Превышено время ожидания установки блокировки». Поэтому длинные транзакции могут сильно снижать удобство параллельной работы пользователей.

Возникновение таких исключений – это повод провести анализ действий, которые выполняются в конфликтующих транзакциях

- возможно, какие-то действия можно вынести за транзакцию (см. п. 2.4);
    
- если действие вынести нельзя, то нужно постараться оптимизировать алгоритм его выполнения;
    
- также нужно проанализировать оптимальность устанавливаемых блокировок (см. группу стандартов **Избыточные блокировки и методы оптимизации**)
    

2.3. В рамках транзакции нужно стремиться выполнять минимум действий – только те, которые нельзя в соответствии с бизнес-логикой выполнять вне транзакции. В частности:

- сложные, ресурсоемкие расчеты нужно стремиться делать до начала транзакции, если это позволяет бизнес-логика;
    
- если расчет должен выполняться в транзакции, то нужно стремиться сделать его как можно более простым. Например, контроль остатков можно делать уже после записи простым запросом к записываемому регистру;
    
- проверка заполнения объекта должна делаться вне транзакции (см. [Проверки, выполняемые в и вне транзакции записи объекта](https://its.1c.ru/db/v8std/content/src/100/400/i8100463.htm_));
    
- запросы, перед выполнением которых не нужно устанавливать блокировку данных, нужно стремиться выполнять до начала транзакции (см. [Ответственное чтение данных](https://its.1c.ru/db/v8std/content/648/hdoc));
    
- запросы, выполняемые в рамках транзакций нужно стремиться оптимизировать (см. группу стандартов **Оптимизация запросов**)
    
- не следует в транзакции вызывать внешние ресурсы (сетевые папки, интранет- и интернет-сайты, почтовые, FTP-, HTTP-, веб-сервисы и т.п.):
    
    - обращение к внешнему ресурсу может быть длительным;
    - ресурс может быть недоступен;
        
    - может произойти ошибка тайм-аута.
        

3. Обязательное использование транзакции.

3.1. В случае, если для ускорения операции записи в регистр используется отключение итогов, такую операцию вместе с отключением и включением итогов необходимо выполнять в транзакции, иначе в других сеансах может возникнуть ошибка при получении среза последних.

Пример:

**Неправильно**

```bsl
РегистрыСведений.КурсыВалют.УстановитьИспользованиеИтогов(Ложь);  
 
НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();  
НаборЗаписей.Отбор.Валюта.Установить(ВалютаСсылка);  
НаборЗаписей.Загрузить(ТаблицаКурсов);  
НаборЗаписей.ОбменДанными.Загрузка = Истина;  
 
НаборЗаписей.Записать();  
 
РегистрыСведений.КурсыВалют.УстановитьИспользованиеИтогов(Истина);  
РегистрыСведений.КурсыВалют.ПересчитатьИтоги();
```

**Правильно**

```bsl
НачатьТранзакцию();  
Попытка  
    РегистрыСведений.КурсыВалют.УстановитьИспользованиеИтогов(Ложь);  
     
    НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();  
    НаборЗаписей.Отбор.Валюта.Установить(ВалютаСсылка);  
    НаборЗаписей.Загрузить(ТаблицаКурсов);  
    НаборЗаписей.ОбменДанными.Загрузка = Истина;  
     
    НаборЗаписей.Записать();  
     
    РегистрыСведений.КурсыВалют.УстановитьИспользованиеИтогов(Истина);  
     
    ЗафиксироватьТранзакцию();  
Исключение  
    ОтменитьТранзакцию();  
    ВызватьИсключение;  
КонецПопытки;  
 
РегистрыСведений.КурсыВалют.ПересчитатьИтоги();
```

#1С #1С/Язык_программирования/Исключения
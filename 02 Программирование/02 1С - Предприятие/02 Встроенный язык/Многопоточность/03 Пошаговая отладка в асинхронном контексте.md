Асинхронный код не выполняется строго линейно, в том порядке, как строки кода следуют друг за другом, поскольку происходят приостановки и возобновления выполнения, возврат управления вызывающему коду.

Для удобства разработчиков в отладчике была реализована “имитация” синхронного выполнения асинхронных функций. Это означает, что при выполнении команд Шагнуть через (F10) и Шагнуть в (F11) на строке, содержащей оператор Ждать, отладчик будет дожидаться завершения работы асинхронной функции, после чего перейдет к следующей строке кода. Хотя фактически при этом произойдет выход из текущего метода и передача управления вызвавшему эту функцию коду. За счет такого поведения “эмулируется” синхронная работа отладчика.

Рассмотрим пример. Есть следующий асинхронный код:

```bsl
&НаКлиенте Асинх Процедура ВывестиОтветНаВопрос(Команда)
    Обещание = ОтветНаВопрос();
    Сообщить("Пользователь выбрал вариант");
    Сообщить(Ждать Обещание);
КонецПроцедуры

&НаКлиенте Асинх Функция ОтветНаВопрос()
    Возврат Ждать ВопросАсинх("Выполнить загрузку?", РежимДиалогаВопрос.ДаНет);
КонецФункции
```

Разработчик в отладчике остановился на строке 10, в которой используется оператор Ждать. При выполнении команды Шагнуть через (F10) в пользовательском режиме будет показан вопрос, а также выведено сообщение из строки 4. После ответа пользователя на вопрос указатель текущей строки кода перемещается на строку 11, хотя фактически строка 4 уже была выполнена.

Еще раз выполним команду Шагнуть через (F10). При этом указатель текущей строки окажется на строке 6, а не на строке 4, как это произошло бы при синхронных вызовах. Получается, что отладчик может оказаться не на той строке, на которой разработчик ожидает остановиться.

Такое поведение объясняется природой асинхронных вызовов, потому что при ожидании на строке 10 выполнение функции ОтветНаВопрос приостановливается, управление передается вызывающем коду в процедуру ВывестиОтветНаВопрос. Выводится сообщение в строке 4. В строке 5 используется оператор Ждать, поэтому происходит приостановка выполнения процедуры ВывестиОтветНаВопрос, управление возвращается платформе.

После того, как пользователь ответит на вопрос, выполнение функции ОтветНаВопрос возобновляется, происходит возврат из этой функции. Ожидание в строке 5 завершается, выводится сообщение с выбранным пользователем вариантом.

Поэтому отладчик показывает, что при выполнении шага со строки 11 указатель текущей строки оказывается на строке 6.

Чтобы разработчику было проще разобраться с порядком вызовов асинхронных функций, в инструменте Стек вызовов цветом выделяются строки. После возобновления асинхронной функции (текущая строка – 11) черным цветом будет выделен стек, выполняющийся в данный момент, а серым – стек, из которого была вызвана асинхронная функция:

Может сложиться ситуация, что исходная процедура, которая вызвала асинхронную функцию, к моменту возобновления работы функции уже завершила свое выполнение. Рассмотрим пример:

```bsl
&НаКлиенте
Асинх Процедура ВывестиОтветНаВопрос(Команда)
    ОтветНаВопрос();
КонецПроцедуры

&НаКлиенте
Асинх Функция ОтветНаВопрос()
    Ответ = Ждать ВопросАсинх("Выполнить загрузку?", РежимДиалогаВопрос.ДаНет);
    Сообщить("Пользователь выбрал вариант");
    Сообщить(Ответ);
КонецФункции
```
Если в отладчике выполнить остановку на строке 11 и сделать шаг (нажатием F10 или F11), то произойдет выход из отладки, в строку 4 выполнение кода не попадет. Почему? Дело в том, что в строке 8 используется оператор Ждать, поэтому происходит приостановка выполнения функции ОтветНаВопрос, управление возвращается к вызывающему коду – процедуре ВывестиОтветНаВопрос. В строке 4 происходит завершение работы процедуры.

Когда пользователь ответит на вопрос, произойдет возобновление выполнения функции в строке 8, будут выведены сообщения из строк 9 и 10. При выходе из функции ОтветНаВопрос в строке 11 возвращаться уже некуда, поскольку вызвавший эту функцию код уже отработал. Но тем не менее в стеке вызовов этот код будет отображаться серым цветом:


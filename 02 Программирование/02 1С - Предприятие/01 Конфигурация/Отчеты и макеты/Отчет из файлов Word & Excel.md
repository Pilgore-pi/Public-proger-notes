
[Источник](https://www.youtube.com/watch?v=zv5Dh2ztAmQ&t=1127s)

## Метод работы с Word через макет "Двоичные данные"

Используются:

* 1С:Предприятие 8.3
* БСП 3.1.5
* Макет печатной формы "Бинарные данные"

Макет представлен документом Word. Данный подход формирует печатную форму и сохраняет ее в документ .docx

Общий алгоритм:

1. Создать макет документа в Word
2. Создать макет печатной формы в 1С с типом "Двоичные данные", загрузив из файла макет Word (шаг 1)
3. Создать / дополнить процедуру `ДобавитьКомандыПечати`. Добавить новую команду печати и задать для нее идентификатор и представление
4. 

Обозначение областей:

```
{v8 Область.ИмяОбласти}
...
{/v8 Область.ИмяОбласти}
```

Обозначение параметров:

```
{v8 ИмяПараметра}
```

В Word существуют следующие виды областей:

* `Общая`
* `СтрокаТаблицы`
* `Список`
* `ВерхнийКолонтитул`
* `НижнийКолонтитул`
* `ВерхнийТитульныйКолонтитул`
* `НижнийТитульныйКолонтитул`
* `ВерхнийЧетныйКолонтитул`
* `НижнийЧетныйКолонтитул`

* Создадим документ Word и зададим в нем области и параметры
* Создадим макет в Конфигураторе типа "Двоичные данные" и загрузим файл макета Word
* Откроем **модуль менеджера** документа, на основе которого будут задаваться параметры
* Создадим процедуру в модуле менеджера:

```bsl
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
    
    // Код для отображения UI команды
    командаПечати = КомандыПечати.Добавить();
    командаПечати.МенеджерПечати = "Документ.ЗаказКлиента";
    командаПечати.Идентификатор = "<ИдентификаторПечатнойФормы>";
    командаПечати.Представление = НСтр("ru = '<Синоним>'");
    командаПечати.ПроверкаПроведенияПередПечатью = ложь;
КонецПроцедуры
```

Данная процедура позволяет отображать `Группу` типа `Подменю` на форме документа, в которой перечислены все добавленные команды с помощью метода `КомандыПечати.Добавить()`

* Добавим процедуру `Печать()` в модуль менеджера документа, которая будет формировать печатную форму:

```bsl
Процедура Печать(
    МассивОбъектов, // ссылки на документы, которые необходимо напечатать
    ПараметрыПечати,
    КоллекцияПечатныхФорм,
    ОбъектыПечати,
    ПараметрыВывода
) Экспорт
    
    печатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(
        КоллекцияПечатныхФорм, "<ИдентификаторПечатнойФормы>"
    );
    
    Если печатнаяФорма <> неопределено тогда
        // тип Соответствие
        печатнаяФорма.ОфисныеДокументы = НапечататьДокумент(МассивОбъектов);
        печатнаяФорма.СинонимМакета = НСтр("ru = 'Синоним макета'");
    конецЕсли;
    
КонецПроцедуры
    
Функция НапечататьДокумент(МассивОбъектов)
    
    // Ключ: Адрес сформированного документа во временном хранилище
    // Значение: Представление печатной формы
    офисныеДоки = новый Соответствие;
    
    // Шаблон имени документа
    Шаблон = "[Организация]-[Контрагент] Документ №[Номер] от [Дата]"
    ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
        МассивОбъектов,
        "<перечисление нужных реквизитов через запятую>, Ссылка"
    );
    
    Для каждого ссылка из МассивОбъектов Цикл
        
        реквизитыДока = ЗначенияРеквизитов[Ссылка];
        реквизитыДока.Дата = Формат(РеквизитыДокумента.Дата, "ДЛФ=D");
        реквизитыДока.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
            РеквизитыДокумента.Номер
        );
        
        ИмяДокумента = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
            Шаблон,
            реквизитыДока
        );
        
        АдресДока = СформироватьДокумент(Ссылка);
        ОфисныеДоки.Вставить(АдресДока, ИмяДокумента);
        
    КонецЦикла;
    
    возврат офисныеДоки;
    
КонецФункции
```

Функция непосредственного заполнения печатной формы:

```bsl
Функция СформироватьДокумент(Ссылка)
    
    // Подготавливаем макет для формирования печатной формы OpenXML
    МакетДокумента = УправлениеПечатью.МакетПечатнойФормы(
        "Документ.МойДокумент.ИдентификаторПечатнойФормы"
    );
    Макет = УправлениеПечатью.ИнициализироватьМакетОфисногоДокумента(
        МакетДокумента,
        Неопределено
    );
	
	// Создаем структуру областей формируемой печатной формы OpenXМL
	ОписаниеОбластей = Новый Структура;
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Заголовок", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Шапка", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ШапкаТаблицы", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "СтрокаТаблицы", "СтрокаТаблицы");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Подвал", "Общая");
	
	// Подготавливаем печатную форму в формате офисного документа
	ПечатнаяФорма = УправлениеПечатью.ИнициализироватьПечатнуюФорму(
    	Неопределено,
    	Неопределено,
    	Макет
    );
    
	// Получаем данные документа
	Запрос = Новый Запрос;
	
	// Пример документа
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиента.Номер КАК Номер,
		|	ЗаказКлиента.Дата КАК Дата,
		|	ЗаказКлиента.Контрагент КАК ПредставлениеЗаказчика,
		|	ЗаказКлиента.Договор КАК Договор,
		|	ЗаказКлиента.СуммаДокумента КАК СуммаДокумента,
		|	ЗаказКлиента.Ответственный КАК Менеджер,
		|	ЗаказКлиента.Комментарий КАК Комментарий,
		|	ЗаказКлиента.Организация КАК ПредставлениеОрганизации,
		|	ЗаказКлиента.Организация.Телефон КАК Телефон,
		|	ЗаказКлиента.Организация.ЭлПочта КАК Email
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Ссылка КАК Ссылка,
		|	ЗаказКлиентаТовары.НомерСтроки КАК НС,
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Количество КАК Количество,
		|	ЗаказКлиентаТовары.ЕдиницаИзмерения КАК ЕдИзм,
		|	ЗаказКлиентаТовары.Цена КАК Цена,
		|	ЗаказКлиентаТовары.СтавкаНДС КАК СтавкаНДС,
		|	ЗаказКлиентаТовары.Сумма КАК Сумма,
		|	ЗаказКлиентаТовары.СуммаНДС КАК СуммаНДС,
		|	ЗаказКлиентаТовары.СуммаВсего КАК СуммаВсего
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ДанныеДляПечати = Запрос.ВыполнитьПакет();
    
	Шапка  = ДанныеДляПечати[0].Выгрузить();
	Товары = ДанныеДляПечати[1].Выгрузить();
	
	ДанныеШапка = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Шапка[0]);
	ДанныеШапка["Номер"] = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеШапка["Номер"]);
	ДанныеШапка["Дата"] = Формат(ДанныеШапка["Дата"], "ДФ=dd.MM.yyyy");
	
	ДанныеТовары = ОбщегоНазначения.ТаблицаЗначенийВМассив(Товары);
	
	// Вывод заголовка
	Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["Заголовок"]);
	УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(
    	ПечатнаяФорма,
    	Область,
    	ДанныеШапка
    );
		
	// Вывод шапки документа
	Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["Шапка"]);
	УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(
    	ПечатнаяФорма,
    	Область,
    	ДанныеШапка
    );
	
	// Вывод таблицы
	Если ДанныеТовары.Количество() > 0 Тогда
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["ШапкаТаблицы"]);
		УправлениеПечатью.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["СтрокаТаблицы"]);
		УправлениеПечатью.ПрисоединитьИЗаполнитьКоллекцию(
    		ПечатнаяФорма,
    		Область,
    		ДанныеТовары
        );
	КонецЕсли;
    
	// Вывод подвала
	Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["Подвал"]);
	УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(
    	ПечатнаяФорма,
    	Область,
    	ДанныеШапка
    );
    
	// Помещаем сформированную печатную форму в соответствие ОфисныеДокументы
	АдресХранилищаПечатнойФормы = УправлениеПечатью.СформироватьДокумент(ПечатнаяФорма);
	
	//удаление временных файлов
	УправлениеПечатью.ОчиститьСсылки(ПечатнаяФорма);	
	УправлениеПечатью.ОчиститьСсылки(Макет);
    
	Возврат АдресХранилищаПечатнойФормы;
	
КонецФункции
```

### Тип КомандыПечати



#1С #1С/Конфигурация/Макеты #MS_Office/Word #MS_Office/Excel